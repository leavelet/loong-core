# vim: set ts=2 sw=2 et:
# Maintainer: 

pkgname=pacman
pkgver=6.0.1
pkgrel=6
pkgdesc="A library-based package manager with dependency support"
arch=('loong64' 'x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base-devel')
depends=('bash' 'glibc' 'libarchive' 'curl' 'gpgme' 'pacman-mirrorlist'
         'gettext' 'gawk' 'coreutils' 'gnupg' 'grep')
makedepends=('meson' 'asciidoc' 'doxygen')
checkdepends=('python' 'fakechroot')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
provides=('libalpm.so')
backup=(etc/pacman.conf
        etc/makepkg.conf)
options=('strip' 'debug')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.xz{,.sig}
        "fix-wkd-lookup.patch::https://gitlab.archlinux.org/pacman/pacman/-/commit/e1246baddd14ec6f4b6270b59bea0e1b639472a7.patch"
        "patch-updatedb.patch::https://gitlab.archlinux.org/pacman/pacman/-/commit/58d678fa289034f1fe2a56adbb5e496b774755fc.patch"
        add-flto-to-LDFLAGS-for-clang.patch
        makepkg-use-ffile-prefix-map-instead-of-fdebug-prefi.patch
        libmakepkg-add-extra-buildflags-only-when-buildflags.patch
        make-link-time-optimization-flags-configurable.patch
        pacman.conf
        makepkg.x86_64.conf
        makepkg.loong64.conf
        makepkg-loongarch64.patch
        pacman-use-loong64.patch
        pacman-compatible-loongarch64.patch)
sha256sums=('0db61456e56aa49e260e891c0b025be210319e62b15521f29d3e93b00d3bf731'
            'SKIP'
            '8ab5b1338874d7d58e11c5d1185ea3454fcc89755f9c18faf87ff348ad1ed16c'
            '988acbb76a0bbd866ab76121191ba4a968bf12970d581fb81b47504c4d998e96'
            '82ff91b85f4c6ceba19f9330437e2a22aabc966c2b9e2a20a53857f98a42c223'
            'b940e6c0c05a185dce1dbb9da0dcbebf742fca7a63f3e3308d49205afe5a6582'
            '7d0aee976c9c71fcf7c96ef1d99aa76efe47d8c1f4451842d6d159ec7deb4278'
            '5b43e26a76be3ed10a69d4bfb2be48db8cce359baf46583411c7f124737ebe6a'
            '606e55f06c297d2b508bc4438890b229a1abaa68b0374a2d7f94c8e7be6792d7'
            '072020e34f2c55b94a9a486829a7eadab0a830ddb4d8e759b0c4e6cf1bde73a6'
            'b3f2ad3f9c95eb4abca7e350ef19e78a7e6cf7aa5018ded3c1bf702897277499'
            'cc28f2e803d39f7b4789471aebf8b6b2b4780f79b4f3b8453e5abcc3772dda98'
            '8a0ca1f6187ad21bcbfa143668dc47fe5582b610a5020ed590fb33cc2969c05b'
            '5efd5a61441764f781fd3a9aae43394418778b3d6fb7baed3f77ab844e15ff5e')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../add-flto-to-LDFLAGS-for-clang.patch
  patch -Np1 -i ../makepkg-use-ffile-prefix-map-instead-of-fdebug-prefi.patch
  patch -Np1 -i ../libmakepkg-add-extra-buildflags-only-when-buildflags.patch
  patch -Np1 -i ../make-link-time-optimization-flags-configurable.patch
  patch -Np1 -i ../fix-wkd-lookup.patch
  patch -Np1 -i ../patch-updatedb.patch
  patch -p1 -i "$srcdir"/makepkg-loongarch64.patch
  patch -p1 -i "$srcdir"/pacman-use-loong64.patch
  patch -p1 -i "$srcdir"/pacman-compatible-loongarch64.patch
}

build() {
  cd "$pkgname-$pkgver"

  meson --prefix=/usr \
        --buildtype=plain \
        -Ddoc=enabled \
        -Ddoxygen=enabled \
        -Dscriptlet-shell=/usr/bin/bash \
        -Dldconfig=/usr/bin/ldconfig \
        build

  meson compile -C build
}

check() {
  cd "$pkgname-$pkgver"

  meson test -C build
}

package() {
  cd "$pkgname-$pkgver"

  DESTDIR="$pkgdir" meson install -C build

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.${CARCH}.conf" "$pkgdir/etc/makepkg.conf"
}
