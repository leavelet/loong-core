# Maintainer: Christian Hesse <mail@eworm.de>

pkgbase=libxcrypt
pkgname=(libxcrypt) #libxcrypt-compat)
pkgver=4.4.28
pkgrel=2
pkgdesc='Modern library for one-way hashing of passwords'
arch=('loong64' 'x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('GPL')
depends=('glibc')
provides=('libcrypt.so')
options=('debug')
install=libxcrypt.install
validpgpkeys=('678CE3FEE430311596DB8C16F52E98007594C21D') # Bj√∂rn 'besser82' Esser
source=("${url}/releases/download/v${pkgver}/${pkgbase}-${pkgver}.tar.xz"{,.asc}
		libxcrypt-la64.patch)
sha256sums=('9e936811f9fad11dbca33ca19bd97c55c52eb3ca15901f27ade046cc79e69e87'
            'SKIP'
            'a6afa6ca8328c031f99d2e49da92b2187a4accddf4519bc7c5e02f80cc5713ca')

prepare() {
  cd "$pkgbase-$pkgver"
  patch -p1 -i "$srcdir/libxcrypt-la64.patch"
}

build() {
  mkdir build-libxcrypt build-libxcrypt-compat

  cd "${srcdir}/build-libxcrypt/"
  "${srcdir}/${pkgbase}-${pkgver}"/configure \
    --prefix=/usr \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=no \
    --disable-failure-tokens
  make
}

check() {
  cd build-libxcrypt/

  make check 
}

package_libxcrypt() {
  cd build-libxcrypt/

  make DESTDIR="${pkgdir}" install
}

package_libxcrypt-compat() {
  pkgdesc='Modern library for one-way hashing of passwords - legacy API functions'
  depends=('libxcrypt')
  
  cd build-libxcrypt-compat/

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,lib/{lib*.so,pkgconfig},share}
}
