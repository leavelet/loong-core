# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Murtuza Akhtari <inxsible at gmail dot com>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

# upstream only signs commits
_commit=e067160ecef8208e1944002e5d50b275733211fb # 17
pkgname=efibootmgr
pkgver=17
pkgrel=2
pkgdesc="Linux user-space application to modify the EFI Boot Manager"
arch=('loongarch64' 'x86_64')
url="https://github.com/rhboot/efibootmgr"
license=('GPL2')
depends=('glibc' 'popt')
makedepends=('efivar' 'git')
source=("${pkgname}::git+https://github.com/rhboot/efibootmgr#tag=${pkgver}?signed"
        efibootmgr-fix-build.patch)
sha512sums=('SKIP'
            '0fab53be54f0e1492b5b650d71e18149099f36c4c88e6d602a12b0f369bd97757e59c40ec6107e2135c093237c328dd718d5212ece79d38870ef0568c421b369')
validpgpkeys=('B00B48BC731AA8840FED9FB0EED266B70F4FEF10') # Peter Jones <pjones@redhat.com>

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # removing hotfix function declaration:
  # https://github.com/rhboot/efibootmgr/issues/128
  sed -e '/extern int efi_set_verbose/d' -i "src/${pkgname}.c"
  patch -p1 -i "$srcdir/efibootmgr-fix-build.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  make libdir='/usr/lib' sbindir='/usr/bin' EFIDIR='arch'
}

package() {
  depends+=('libefiboot.so' 'libefivar.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" \
       libdir='/usr/lib' \
       sbindir='/usr/bin' \
       EFIDIR='arch' \
       install
  install -vDm 644 {AUTHORS,README,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
