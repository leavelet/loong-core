# Maintainer: Daniel Isenmann <daniel [at] archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgname=gc
pkgver=8.2.2
pkgrel=1
pkgdesc="A garbage collector for C and C++"
arch=('loong64' 'x86_64')
url="https://www.hboehm.info/gc/"
license=('GPL')
depends=('gcc-libs')
source=(https://github.com/ivmai/bdwgc/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        gc-missing-header.patch::https://github.com/ivmai/bdwgc/commit/c876dc2b.patch
        bdwgc-la64.patch)
sha512sums=('4a7b26789ce22ab72bfaadf3029362c5fe26737df1e856e43db7d9b24ee8acf625e35d596bb3f698f91d6a5ddfb6c45a952a1dbd18d47359569696a544c9c248'
            'fc1bb2c8b412f22d6c861c2f89f8cf8ed3b8f053f935a766aad80ea05016401b72580250f37afd180283090fedc4e6257272c8fab610130d13b67cacbd445142'
            'b637c0daf8de1dea639b1cf2914ca8aa1172783d95c68bcd664c61af9438130f903b2b2bc68bb4a3d0d07da1044c4a8a5bc283bd8ee38a7fe8eecbce8a0a50fc')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../gc-missing-header.patch # Install missing header
  patch -p1 < ../bdwgc-la64.patch
  ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-cplusplus --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  sed 's|GC_MALLOC 1L|gc 3|g' doc/gc.man |
    install -Dm644 /dev/stdin "${pkgdir}/usr/share/man/man3/gc.3"
}
