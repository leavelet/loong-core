# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.4.3
pkgrel=1
pkgdesc='Portable foreign function interface library'
arch=('loong64' 'x86_64')
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
provides=('libffi.so')
options=('debug')
source=(https://github.com/libffi/libffi/releases/download/v$pkgver/libffi-$pkgver.tar.gz
        libffi-la64.patch)
sha256sums=('4416dd92b6ae8fcb5b10421e711c4d3cb31203d77521a77d85d0102311e6c3b8'
            'acc52fd3cf59845b97d13402e1ea6ebb17cbf4dbfc3f5dd5e2bdfe873bd187cb')
b2sums=('5e751c53a6b65316e438723810fbafe7f27732feb50466f1459d086c35a519f460b57968721212496a7502b0a5860546c84b22ec269e979728f18d0731fc918a'
        'bf8a5a995f7ae7e73673b2989e6de0db04304336d60797bee405a9fdb7024ed05c4662cdad861981eb62554fba43bdef1133b3df2665763d78b2204f13a1aa0f')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir/libffi-la64.patch"
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  # remove --disable-exec-static-tramp once ghc and gobject-introspection
  # work fine with it enabled (https://github.com/libffi/libffi/pull/647)
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-multi-os-directory \
    --disable-exec-static-tramp \
    --enable-pax_emutramp
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
}

# vim:set ts=2 sw=2 et:
