# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=35.2
pkgrel=1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'zstd' 'systemd')
checkdepends=('bash-bats' 'bash-bats-assert' 'lzop')
optdepends=('gzip: Use gzip compression for the initramfs image'
            'xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        "mkinitcpio-la64.patch"
        "mkinitcpio-zboot.patch")
install=mkinitcpio.install
sha512sums=('9ca8544bc5de3d215bf94ecf9a3bfb9265c9ed9871357af4b597a37e94c6951935641c11558470aaa934e8fbe9bc1f875dfa9d0fe8734a97f90154c9cbabfdba'
            'SKIP'
            'e521ad748fc73bd788e1fcf52ee11fd3c87e88dfa44072698b3f7925bd5fc9975e18b15373f6926ae5aa59ead378d2633a9967a71db1588e69754b939e56223c'
            '0b7c01a1feb32e1dcd4bc83036767e45f3953635ee0417fb1048c919912448769acead7cfc6e64fb7bbd84e4951be1acae9c02786cb685b3a756676165779fec')
b2sums=('e7d56ea1de8d21271d4e94a61563af9c5f33320a46ce9f006092bb70e2161be02791dfd75b14e9c9b5ad2fbb781617f04b4e49afa93a707785c23773c1a48664'
        'SKIP'
        '52e6b1b516fb49faeadb0c3b1bf7adb55b276b52097bfb3b8638bcea1a33a4dbf10ccbb266747275285354e0bc766597874b4c0c85f9fbd3c326f459970a8500'
        '0fd2de095e52f9898d7eeeee580430afc0cfe9c0cad3e96f31e7f3504ceb9e9865f2410f9e0c1dfd5f213ef06769f97fae294dd776cb157a4e16693cd63024cc')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  patch -p1 -d "$pkgname-$pkgver" -i $srcdir/mkinitcpio-la64.patch
  patch -p1 -d "$pkgname-$pkgver" -i $srcdir/mkinitcpio-zboot.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
